plugins {
    id 'com.android.application'
    id 'com.google.protobuf' version '0.9.0'
}

def osName = System.getProperty("os.name").toLowerCase()
def osArch = System.getProperty("os.arch")

if (osName.contains("mac") && osArch.equals("aarch64")) {
    project.ext.osArch = "osx-x86_64"
} else {
    project.ext.osArch = ""
}

android {
    compileSdk 34

    defaultConfig {
        applicationId "net.discdd.bundletransport"
        minSdk 26
        targetSdk 34
        versionCode 1
        versionName "1.0"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    buildTypes {
        debug {
            minifyEnabled false
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    // Ensure namespace consistency
    namespace 'net.discdd.bundletransport'

    lint {
        abortOnError false
        disable 'GoogleAppIndexingWarning', 'HardcodedText', 'InvalidPackage'
        textOutput file('stdout')
        textReport true
    }
}

dependencies {
    implementation 'io.grpc:grpc-netty-shaded:1.58.0'
    implementation 'io.grpc:grpc-protobuf:1.58.0'
    implementation 'io.grpc:grpc-stub:1.58.0'
    implementation 'io.grpc:grpc-okhttp:1.58.0'
    implementation 'com.google.firebase:firebase-crashlytics-buildtools:3.0.2'
    implementation 'com.google.protobuf:protobuf-java:3.23.4'
    compileOnly 'org.apache.tomcat:annotations-api:6.0.53' // necessary for Java 9+

    // Exclude protobuf-java from signal-protocol-android to avoid conflicts
    implementation('org.whispersystems:signal-protocol-android:2.2.0') {
        exclude group: 'com.google.protobuf', module: 'protobuf-java'
    }

    // Assuming net.discdd:bundle-core depends on protobuf-java
    implementation 'net.discdd:bundle-core:0.0.2'

    def work_version = "2.7.0"
    implementation "androidx.work:work-runtime:$work_version"

    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.1'
    implementation 'androidx.lifecycle:lifecycle-common-java8:2.3.1'

    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}
